dnl Process this file with autoconf to produce a configure script.
dnl
dnl $Id: configure.in,v 1.5 2001-05-20 19:34:43 jle Exp $

dnl -----------------------
dnl Autoconf initialization
dnl -----------------------
AC_INIT(src/MainView.hpp)
AC_PREFIX_DEFAULT(/usr/local)
AC_LANG_CPLUSPLUS

dnl -----------------------
dnl Automake initialization
dnl -----------------------
AM_INIT_AUTOMAKE(vrfig, 0.1)

dnl ------------
dnl C++ settings
dnl ------------
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_RANLIB

dnl -----------------------------
dnl Check for X headers/libraries
dnl -----------------------------
AC_PATH_XTRA
if test "$no_x" = yes; then
  AC_MSG_ERROR([Could not locate X Window System include files and libraries (us
e --x-includes and --x-libraries to specify correct directories).])
fi
test -z "$X_CFLAGS" || CFLAGS="$CFLAGS $X_CFLAGS"
test -z "$X_EXTRA_LIBS" || LIBS="$X_EXTRA_LIBS $LIBS"
LIBS="-lXext -lX11 $LIBS"
test -z "$X_PRE_LIBS" || LIBS="$X_PRE_LIBS $LIBS"
test -z "$X_LIBS" || LDFLAGS="$LDFLAGS $X_LIBS"

dnl --------------------------------
dnl Check for FLTK headers/libraries
dnl --------------------------------
AC_CHECK_HEADER(FL/Fl.H,, AC_MSG_ERROR([Could not find FLTK include files.]))
__fltk_ok=no
AC_CHECK_LIB(fltk, main, LIBS="-lfltk $LIBS"; __fltk_ok=yes,, $LDFLAGS $LIBS)
if test "$__fltk_ok" = "no"; then
  AC_MSG_WARN([Could not link with FLTK, checking if -lfltk requires -lGL.])
  unset ac_cv_lib_fltk_main
  LIBS="-lGL $LIBS"
  AC_CHECK_LIB(fltk, main, LIBS="-lfltk $LIBS",
               AC_MSG_ERROR([Could not link with FLTK library.]),
               $LDFLAGS $LIBS)
  ac_cv_lib_fltk_main=no
fi

dnl ----------------------------------------------------
dnl Check if Agenda Computing version of FLTK being used
dnl ----------------------------------------------------
AC_MSG_CHECKING(for FLTK with Agenda Computing patches)
AC_TRY_LINK([#include <FL/Fl_Repeat_Button.H>],
  [Fl_Repeat_Button *rp = 0;
   rp->repeat(0, 0);],
  CPPFLAGS="$CPPFLAGS -DHAVE_AGENDA_FLTK"; AC_MSG_RESULT(yes),
  AC_MSG_RESULT(no))

dnl --------------------------------
dnl Check for Flek headers/libraries
dnl --------------------------------
AC_CHECK_HEADER(Flek/Fl_App_Window.H,,
                AC_MSG_ERROR([Could not find Flek include files.]))
AC_CHECK_LIB(flek_ui, main, LIBS="-lflek_ui -lflek_core $LIBS",
             AC_MSG_ERROR([Could not link with Flek library.]),
             $LDFLAGS -lflek_core $LIBS)

dnl ---------------------------------
dnl Check for flpda headers/libraries
dnl ---------------------------------
AC_CHECK_HEADER(flpda/Widget_Factory.h,,
                AC_MSG_ERROR([Could not find flpda include files.]))
AC_CHECK_LIB(flpda, main, LIBS="-lflpda $LIBS",
             AC_MSG_ERROR([Could not link with flpda library.]),
             $LDFLAGS $LIBS)

dnl --------------------------------------------
dnl Check for expat XML parser headers/libraries
dnl --------------------------------------------
AC_CHECK_HEADER(expat.h,,
                AC_MSG_ERROR([Could not find expat include files.]))
AC_CHECK_LIB(expat, main, LIBS="-lexpat $LIBS",
             AC_MSG_ERROR([Could not link with expat library.]),
             $LDFLAGS $LIBS)

dnl -------------------------------------------------------------------
dnl Enable maintainer mode, if configured with --enable-maintainer-mode
dnl -------------------------------------------------------------------
AM_MAINTAINER_MODE

dnl ------------------
dnl Write output files
dnl ------------------
AC_OUTPUT(
  Makefile
  src/Makefile
  src/elements/Makefile
  src/tools/Makefile
  src/espws/Makefile
)
